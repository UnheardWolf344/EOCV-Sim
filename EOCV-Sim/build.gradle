import java.nio.file.Paths
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow'
    id 'maven-publish'
}

apply from: '../build.common.gradle'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

ext.kotest_version = '5.7.2'

test {
    useJUnitPlatform()
}

apply from: '../test-logging.gradle'

dependencies {
    api project(':Common')
    api project(':Vision')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    implementation "org.eclipse.jdt:ecj:3.21.0"

    api "org.openpnp:opencv:$opencv_version"

    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation "org.apache.logging.log4j:log4j-api:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-core:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"

    implementation "com.github.deltacv:steve:1.0.0"
    implementation "com.github.deltacv.AprilTagDesktop:AprilTagDesktop:$apriltag_plugin_version"

    implementation 'info.picocli:picocli:4.6.1'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation "io.github.classgraph:classgraph:$classgraph_version"

    implementation 'com.formdev:flatlaf:3.5.1'
    implementation 'com.formdev:flatlaf-intellij-themes:3.5.1'

    implementation 'net.lingala.zip4j:zip4j:2.11.3'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-swing:$kotlinx_coroutines_version"

    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core:$kotest_version"

    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'com.google.jimfs:jimfs:1.3.0'

    implementation 'org.ow2.asm:asm:9.7'
}

task(writeBuildClassJava) {

    String date = DateTimeFormatter.ofPattern("yyyy-M-d hh:mm:ss").format(LocalDateTime.now())

    File versionFile = Paths.get(
        projectDir.absolutePath, 'src', 'main', 'java',
        'com', 'github', 'serivesmejia', 'eocvsim', 'Build.java'
    ).toFile()

    versionFile.delete()

    versionFile << "package com.github.serivesmejia.eocvsim;\n" +
        "\n" +
        "/*\n" +
        " * Autogenerated file! Do not manually edit this file, as\n" +
        " * it is regenerated any time the build task is run.\n" +
        " *\n" +
        " * Based from PhotonVision PhotonVersion generator task\n"+
        " */\n" +
        "@SuppressWarnings(\"ALL\")\n" +
        "public final class Build {\n" +
        "    public static final String versionString = \"$version\";\n" +
        "    public static final String standardVersionString = \"$standardVersion\";\n" +
        "    public static final String buildDate = \"$date\";\n" +
        "    public static final boolean isDev = ${version.contains("dev")};\n\n" +
        "    public static final String opencvVersion = \"$opencv_version\";\n" +
        "    public static final String apriltagPluginVersion = \"$apriltag_plugin_version\";\n" +
        "}"
}

build.dependsOn writeBuildClassJava
